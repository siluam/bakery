#+setupfile: ./settings/README.org
#+property: header-args:hy :tangle yes

* Preparations
** Rich Traceback

#+begin_src hy
(import richy.traceback)
(.install richy.traceback)
#+end_src

** Imports
*** From

#+begin_src hy
(import oreo [eclair either? nots?])
(import os [path :as osPath])
#+end_src

** Requires

#+begin_src hy
(require hyrule [->])
#+end_src

** Zoom Decorator

#+begin_src hy
(setv funcs [])
(defn zoom [func] (.append funcs func))
#+end_src

** Cookies!

#+begin_src hy
(setv cookies (.join osPath (.dirname osPath (.realpath osPath __file__)) "cookies"))
#+end_src

* Tests
** List Output

#+begin_src hy
#@(zoom (defn list-output []
              (import bakery [ls])
              (-> cookies
                  (ls :m/list True)
                  (isinstance list)
                  (assert))
              (-> cookies
                  (ls :m/type list)
                  (isinstance list)
                  (assert))))
#+end_src

** Sorted Output

# TODO: Test sorting strings, by using :m/frosting True and :m/type frosting
# TODO: Test sorting with lambdas

#+begin_src hy
#@(zoom (defn list-output []
              (import bakery [ls])
              (-> (ls)
                  (sorted)
                  (= (ls :m/list True :m/sort None))
                  (assert))))
#+end_src

** Main

#+begin_src hy
#@(zoom (defn main []
              (import bakery [ls])
              (import os [listdir])
              (-> cookies
                  (ls :m/list True)
                  (.sort)
                  (= (.sort (lfor item (listdir cookies) :if (not (.startswith item ".")) item)))
                  (assert))))
#+end_src

** Program Options

#+begin_src hy
#@(zoom (defn program-options []
              (import bakery [ls])
              (import os [listdir])
              (-> cookies
                  (ls :m/list True :a True)
                  (.sort)
                  (= (.sort (listdir cookies)))
                  (assert))))
#+end_src

** Context Manager

Check if temporary baking with a context manager works:

#+begin_src hy
#@(zoom (defn context-manager []
              (import bakery [ls])
              (import os [listdir])
              (with [lsa (ls :a True cookies :m/context True :m/list True)]
                    (-> (lsa)
                        (.sort)
                        (= (.sort (listdir cookies)))
                        (assert)))
              (-> cookies
                  (ls :m/list True)
                  (.sort)
                  (= (.sort (lfor item (listdir cookies) :if (not (.startswith item ".")) item)))
                  (assert))))
#+end_src

** Loop

#+begin_src hy
#@(zoom (defn loop []
              (import bakery [ls])
              (import os [listdir chdir getcwd])
              (setv output (listdir cookies)
                    cwd (getcwd))
              (try (chdir cookies)
                   (for [item ls] (assert (in item output)))
                   (finally (chdir cwd)))))
#+end_src

** Progress

#+begin_src hy
#@(zoom (defn progress []
              (import bakery [ls])
              (import os [listdir])
              (setv output (listdir cookies))
              (for [item (ls :a True cookies :m/progress "red")]
                   (if (nots? item)
                       (assert (in item output))))))
#+end_src

** Baking

#+begin_src hy
#@(zoom (defn baking []
              (import bakery [ls])
              (import os [listdir])
              (.bake- ls :a True :m/progress "green" cookies)
              (setv output (listdir cookies))
              (for [item ls]
                   (if (nots? item)
                       (assert (in item output))))))
#+end_src

** Module Call

#+begin_src hy
#@(zoom (defn module-call []
              (import bakery)
              (import os [listdir])
              (setv ls (bakery :program- "ls"))
              (-> cookies
                  (ls :a True :m/list True)
                  (.sort)
                  (= (.sort (listdir cookies)))
                  (assert))))
#+end_src

** Freezing

#+begin_src hy
#@(zoom (defn freezing []
              (import bakery)
              (import bakery [ls])
              (-> []
                  (ls)
                  (either? bakery)
                  (assert))))
#+end_src

** Git Remote

#+begin_src hy
#@(zoom (defn git-status []
              (import bakery [git])
              (-> (git :C cookies)
                  (.remote :m/str True)
                  (= "origin")
                  (assert))))
#+end_src

** String Output

#+begin_src hy
#@(zoom (defn string-output []
              (import bakery [echo])
              (-> "Hello!"
                  (echo :m/str True)
                  (= "Hello!")
                  (assert))))
#+end_src

** Piping
*** First

#+begin_src hy
#@(zoom (defn piping/first []
              (import bakery [ls tail])
              (import os [listdir])
              (setv tails (| (ls [] :a True cookies :m/list True) tail)
                    output (.sort (tails)))
              #_(-> cookies
                  (listdir)

                  ;; TODO: Why does this end at -1? Cut doesn't work like slices
                  (cut 10 -1))

              (assert (= output (.sort (cut (listdir cookies) 10 -1))))))
#+end_src

*** Both

#+begin_src hy
#@(zoom (defn piping/both []
              (import bakery [env grep])
              (setv egrep (| (env [] :m/exports { "FOO" "bar" } :m/str True) (grep [] "FOO")))
              (assert (= (egrep) "FOO=bar"))))
#+end_src

** Exports

#+begin_src hy
#@(zoom (defn exports []
              (import bakery [echo])
              (-> "$FOO"
                  (echo :m/exports { "FOO" "bar" } :m/str True)
                  (= "bar")
                  (assert))))
#+end_src

** Number of Lines of Output

#+begin_src hy
#@(zoom (defn trim []
              (import bakery [cat])
              (setv bulbasaur (.sort [ "001: Bulbasaur" "002: Ivysaur" "003: Venusaur" ])
                    last-three (.sort [ "058: Growlithe" "059: Arcanine" "060: Poliwag" ]))

              (-> cookies
                  (+ "/01")
                  (cat :m/n-lines 3 :m/list True)
                  (.sort)
                  (= bulbasaur)
                  (assert))

              (-> cookies
                  (+ "/01")
                  (cat :m/n-lines (, 3) :m/list True)
                  (.sort)
                  (= bulbasaur)
                  (assert))

              (-> cookies
                  (+ "/01")
                  (cat :m/n-lines { "number" 3 } :m/list True)
                  (.sort)
                  (= bulbasaur)
                  (assert))

              (-> cookies
                  (+ "/01")
                  (cat :m/n-lines (, True 3) :m/list True)
                  (.sort)
                  (= last-three)
                  (assert))

              (-> cookies
                  (+ "/01")
                  (cat :m/n-lines { "last" True "number" 3 } :m/list True)
                  (.sort)
                  (= last-three)
                  (assert))))
#+end_src

* Run Everything

#+begin_src hy
(for [func (eclair funcs "tests" "blue")] (func))
#+end_src