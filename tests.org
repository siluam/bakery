#+setupfile: ./settings/README.org
#+property: header-args:hy :tangle yes

* Preparations
** Rich Traceback

#+begin_src hy
(import rich.traceback)
(.install rich.traceback)
#+end_src

** Imports
*** From

#+begin_src hy
(import oreo [eclair either? nots?])
(import os [path :as osPath])
#+end_src

** Requires

#+begin_src hy
(require hyrule [->])
#+end_src

** Zoom Decorator

#+begin_src hy
(setv funcs [])
(defn zoom [func] (.append funcs func))
#+end_src

** Cookies!

#+begin_src hy
(setv cookies (.join osPath (.dirname osPath (.realpath osPath __file__)) "cookies"))
#+end_src

* Tests
** List Output

#+begin_src hy
#@(zoom (defn list-output []
              (import bakery [ls])
              (-> cookies
                  (ls :m/list True)
                  (isinstance list)
                  (assert))
              (-> cookies
                  (ls :m/type list)
                  (isinstance list)
                  (assert))))
#+end_src

** Sorted Output

# TODO: Test sorting strings, by using :m/frosting True and :m/type frosting
# TODO: Test sorting with lambdas

#+begin_src hy
#@(zoom (defn sorted-output []
              (import bakery [ls])
              (-> (ls)
                  (sorted)
                  (= (ls :m/list True :m/sort None))
                  (assert))))
#+end_src

** Filtered Output

#+begin_src hy
#@(zoom (defn filtered-output []
              (import bakery [ls])
              (setv sixteen [ "11" "12" "13" "14" "15" "16" ])
              (-> cookies
                  (ls :m/list True :m/filter (fn [item] (and (.isnumeric item) (not (in "0" item)))))
                  (= sixteen)
                  (assert))
              (-> cookies
                  (ls :m/list True :m/filter (, True (fn [item] (or (not (.isnumeric item)) (in "0" item)))))
                  (= sixteen)
                  (assert))))
#+end_src

** Main

#+begin_src hy
#@(zoom (defn main []
              (import bakery [ls])
              (import os [listdir])
              (assert (= (ls :m/list True cookies :m/sort None :m/filter (, True (fn [item] (.startswith item "."))))
                         (sorted (gfor item (listdir cookies) :if (not (.startswith item ".")) item))))))
#+end_src

** Program Options

#+begin_src hy
#@(zoom (defn program-options []
              (import bakery [ls])
              (import os [listdir])
              (-> cookies
                  (listdir)
                  (sorted)
                  (= (ls :m/list True :a True cookies :m/sort None :m/filter nots?))
                  (assert))))
#+end_src

** Context Manager

Check if temporary baking with a context manager works:

#+begin_src hy
#@(zoom (defn context-manager []
              (import bakery [ls])
              (import os [listdir])
              (with [lsa (ls :a True cookies :m/context True :m/list True :m/sort None :m/filter nots?)]
                    (-> cookies
                        (listdir)
                        (sorted)
                        (= (lsa))
                        (assert)))
              (-> cookies
                  (ls :m/list True :m/sort None :m/filter (, True (fn [item] (.startswith item "."))))
                  (= (sorted (gfor item (listdir cookies) :if (not (.startswith item ".")) item)))
                  (assert))))
#+end_src

** Loop

#+begin_src hy
#@(zoom (defn loop []
              (import bakery [ls])
              (import os [listdir chdir getcwd])
              (setv output (listdir cookies)
                    cwd (getcwd))
              (try (chdir cookies)
                   (for [item ls] (assert (in item output)))
                   (finally (chdir cwd)))))
#+end_src

** Progress

#+begin_src hy
#@(zoom (defn progress []
              (import bakery [ls])
              (import os [listdir])
              (setv output (listdir cookies))
              (for [item (ls :a True cookies :m/progress "red")]
                   (if (nots? item)
                       (assert (in item output))))))
#+end_src

** Baking

#+begin_src hy
#@(zoom (defn baking []
              (import bakery [ls])
              (import os [listdir])
              (.bake- ls :a True :m/progress "green" cookies)
              (setv output (listdir cookies))
              (for [item ls]
                   (if (nots? item)
                       (assert (in item output))))))
#+end_src

** Module Call

#+begin_src hy
#@(zoom (defn module-call []
              (import bakery)
              (import os [listdir])
              (setv ls (bakery :program- "ls"))
              (-> cookies
                  (listdir)
                  (sorted)
                  (= (ls :a True :m/list True :m/sort None :m/filter nots? cookies))
                  (assert))))
#+end_src

** Freezing

#+begin_src hy
#@(zoom (defn freezing []
              (import bakery)
              (import bakery [ls])
              (-> []
                  (ls)
                  (either? bakery)
                  (assert))))
#+end_src

** Git Remote

#+begin_src hy
#@(zoom (defn git-status []
              (import bakery [git])
              (-> (git :C cookies)
                  (.remote :m/str True)
                  (= "origin")
                  (assert))))
#+end_src

** String Output

#+begin_src hy
#@(zoom (defn string-output []
              (import bakery [echo])
              (-> "Hello!"
                  (echo :m/str True)
                  (= "Hello!")
                  (assert))))
#+end_src

** Piping
*** First

#+begin_src hy
#@(zoom (defn piping/first []
              (import bakery [ls tail])
              (import oreo [first-last-n])
              (import os [listdir])
              (setv tails (| (ls [] :a True cookies :m/list True :m/sort None :m/filter nots? :m/shell "xonsh") tail))
              (-> cookies
                  (listdir)
                  (sorted)
                  (first-last-n :last True :number 10 :type- list)
                  (= (tails))
                  (assert))))
#+end_src

*** Both

#+begin_src hy
#@(zoom (defn piping/both []
              (import bakery [env grep])
              (setv egrep (| (env [] :m/exports { "FOO" "bar" } :m/str True) (grep [] "FOO")))
              (assert (= (egrep) "FOO=bar"))))
#+end_src

** Exports

#+begin_src hy
#@(zoom (defn exports []
              (import bakery [echo])
              (-> "$FOO"
                  (echo :m/exports { "FOO" "bar" } :m/str True)
                  (= "bar")
                  (assert))))
#+end_src

** Number of Lines of Output

#+begin_src hy
#@(zoom (defn trim []
              (import bakery [cat])
              (setv bulbasaur (sorted (, "001: Bulbasaur" "002: Ivysaur" "003: Venusaur" ))
                    last-three (sorted (, "058: Growlithe" "059: Arcanine" "060: Poliwag" )))
              (.bake- cat :m/list True :m/sort None)

              (print (cat (+ cookies "/01") :m/n-lines 3))

              (-> cookies
                  (+ "/01")
                  (cat :m/n-lines 3)
                  (= bulbasaur)
                  (assert))

              (-> cookies
                  (+ "/01")
                  (cat :m/n-lines (, 3))
                  (= bulbasaur)
                  (assert))

              (-> cookies
                  (+ "/01")
                  (cat :m/n-lines { "number" 3 })
                  (= bulbasaur)
                  (assert))

              (-> cookies
                  (+ "/01")
                  (cat :m/n-lines (, True 3))
                  (= last-three)
                  (assert))

              (-> cookies
                  (+ "/01")
                  (cat :m/n-lines { "last" True "number" 3 })
                  (= last-three)
                  (assert))))
#+end_src

** Split

#+begin_src hy
#@(zoom (defn splits []
              (import bakery [ls])
              (setv six-two ["6" "5" "4" "3" "2" "09" "08" "07" "06" "05" "04" "03" "02" "00" "0" "0"])
              (-> cookies
                  (ls :m/list True :m/split 1 :m/sort True :m/filter (fn [item] (.isnumeric item)))
                  (= six-two)
                  (assert))))
#+end_src

** Return `False' Instead of Raising `SystemError'

#+begin_src hy
#@(zoom (defn false-error []
              (import bakery [ls])
              (-> (ls :j True :m/false-error True)
                  (not)
                  (assert))))
#+end_src

*** Return Another Value Instead of Raising `SystemError'

#+begin_src hy
#@(zoom (defn replace-error []
              (import bakery [ls])
              (assert (ls :j True :m/replace-error True))
              (-> (ls :j True :m/replace-error "replace error test")
                  (= "replace error test")
                  (assert))))
#+end_src

* Run Everything

#+begin_src hy
(for [func (eclair funcs "tests" "blue")] (func))
#+end_src