#+setupfile: ./settings/README.org
#+property: header-args:hy :tangle yes

* Preparations
** Rich Traceback

#+begin_src hy
(import rich.traceback)
(.install rich.traceback)
#+end_src

** Imports
*** From

#+begin_src hy
(import oreo [eclair either?])
(import os [path :as osPath])
#+end_src

** Zoom Decorator

#+begin_src hy
(setv funcs [])
(defn zoom [func] (.append funcs func))
#+end_src

** Cookies!

#+begin_src hy
(setv cookies (.join osPath (.dirname osPath (.realpath osPath __file__)) "cookies"))
#+end_src

** Not Dots?

#+begin_src hy
(defn nots? [string] (not (or (= string ".") (= string ".."))))
#+end_src

* Tests
** Main

#+begin_src hy
#@(zoom (defn main []
              (import bakery [ls])
              (import os [listdir])
              (setv output (ls cookies))
              (assert (all (gfor item (listdir cookies) :if (and (not (.startswith item "."))
                                                                   (in item output)) item)))))
#+end_src

** Program Options

#+begin_src hy
#@(zoom (defn program-options []
              (import bakery [ls])
              (import os [listdir])
              (setv output (ls :a True cookies))
              (assert (all (gfor item (listdir cookies) :if (in item output) item)))))
#+end_src

** Context Manager

Check if temporary baking with a context manager works:

#+begin_src hy
#@(zoom (defn context-manager []
              (import bakery [ls])
              (import os [listdir])
              (with [lsa (ls :a True cookies :m/context True)]
                    (setv output (lsa))
                    (assert (all (gfor item (listdir cookies) :if (in item output) item))))
              (setv output (ls cookies))
              (assert (all (gfor item (listdir cookies) :if (and (not (.startswith item "."))
                                                                   (in item output)) item)))))
#+end_src

** Loop

#+begin_src hy
#@(zoom (defn loop []
              (import bakery [ls])
              (import os [listdir chdir getcwd])
              (setv output (listdir cookies)
                    cwd (getcwd))
              (try (chdir cookies)
                   (for [item ls]
                        (assert (in item output)))
                   (finally (chdir cwd)))))
#+end_src

** Progress

#+begin_src hy
#@(zoom (defn progress []
              (import bakery [ls])
              (import os [listdir])
              (setv output (listdir cookies))
              (for [item (ls :a True cookies :m/progress "red")]
                   (if (nots? item)
                       (assert (in item output))))))
#+end_src

** Baking

#+begin_src hy
#@(zoom (defn baking []
              (import bakery [ls])
              (import os [listdir])
              (.bake- ls :a True :m/progress "green" cookies)
              (setv output (listdir cookies))
              (for [item ls]
                   (if (nots? item)
                       (assert (in item output))))))
#+end_src

** Module Call

#+begin_src hy
#@(zoom (defn module-call []
              (import bakery)
              (import os [listdir])
              (setv ls (bakery :program- "ls")
                    output (ls :a True cookies))
              (assert (all (gfor item (listdir cookies) :if (in item output) item)))))
#+end_src

** Freezing

#+begin_src hy
#@(zoom (defn freezing []
              (import bakery)
              (import bakery [ls])
              (assert (either? (ls []) bakery))))
#+end_src

** Git Status

#+begin_src hy
#@(zoom (defn git-status []
              (import bakery [git])
              (assert (= (.status (git :C cookies) :m/str True)
                         "On branch main\nYour branch is up to date with 'origin/main'.\n\nnothing to commit, working tree clean"))))
#+end_src

** String Output

#+begin_src hy
#@(zoom (defn string-output []
              (import bakery [echo])
              (assert (= (echo :m/str True "Hello!") "Hello!"))))
#+end_src

** Piping
*** First

#+begin_src hy
#@(zoom (defn piping/first []
              (import bakery [ls tail])
              (import os [listdir])
              (setv tails (| (ls [] :a True cookies) tail)
                    output (tails))
              (assert (all (gfor item (cut (listdir cookies) 10 -1) :if (in item output) item)))))
#+end_src

*** Both

#+begin_src hy
#@(zoom (defn piping/both []
              (import bakery [env grep])
              (setv egrep (| (env [] :m/exports { "FOO" "bar" } :m/str True) (grep [] "FOO")))
              (assert (= (egrep) "FOO=bar"))))
#+end_src

** Exports

#+begin_src hy
#@(zoom (defn exports []
              (import bakery [echo])
              (assert (= (echo :m/exports { "FOO" "bar" } "$FOO" :m/str True) "bar"))))
#+end_src

* Run Everything

#+begin_src hy
(for [func (eclair funcs "tests" "blue")] (func))
#+end_src